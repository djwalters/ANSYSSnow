/COM
/COM ****************************************
/COM ANSYS Mechanical APDL input wrapper script
/COM
/COM Author: David J Walters
/COM         Montana State University
/COM Version 1.0, 2014-10-17
/COM
/COM
/COM Snow microstructure mechanical analysis utilizing yield strength as a damage criteria
/COM Loading is performed in any direction desired.
/COM NOTE: Academic License limits analyses to either 250k nodes or elements (whichever occurs first)
/COM
/COM This file is the main wrapper script that initializes the appropriate parameters in ANSYS, imports a mesh, and calls the input
/COM for the main analysis routine stored in a static location.
/COM
/COM After importing an ANSYS mesh file (*.cdb), the main routine applies material properties, and appropriate initial boundary conditions.
/COM Solution process runs iteratively inspecting elements for exceeding yield strength following every solution step.
/COM If elements exceed yield, they are "Killed" from the analysis and new load steps are applied.
/COM The analsysis continues until 2 seperate bodies are created. Contact
/COM ****************************************
/COM

! Quick restart is as follows:
! FINISH$/CLE$/INPUT,File,ext
/Batch
/SHOW,PNG

!********************************************
!********************************************
! Adjust the following parameter for the appropriate jobname
/filname,0930Shear,0
!********************************************
!********************************************

/UIS,MSGPOP,4	!Turns off error messages
!/NERR,,,,0,0
/NERR,,,-1

!Store the jobname as a string of any length for saving the results database 
!later.  This is necessary for proper restart following and EKILL operation.
*DIM,JobString,STRING,100,1,1
*DO,s,1,100,8
*get,JobStr,ACTIVE,,JOBNAM,,START,s
JobString(s)=JobStr
*ENDDO

/UNIT,MPA	! Input units switched to mm,tonne(Mg),seconds,celcius (TOFFSET 273.0),Newton,millijoules,MPa
		! Density now in Mg/mm^3

/COM
/COM ****************************************
/COM Read in ANSYS mesh (*.cdb)
/COM

!Windows Input
!/INPUT,'0930_4x4x6Volume','cdb','C:\Doctoral Researach\Mechanical Testing\Radiation Recrystallization\Fabric Tensor and ANSYS\Linux FTP Mirror\PhD Work\MicroMechanics\FEA Files\ANSYS\2013-12-13 Shear New\0930\Mimics Data\',, 0 
   
!Linux Input
/INPUT,'0930_4x4x6Volume','cdb','/home/djwalters/PhDWork/ANSYS/2013-12-13ShearNew/0930/MimicsData',, 0

! Note: Input file from Materialise export to ANSYS should already contain volume elements defaulted to element
! type 1.  In the case of the 4 node tet, it is SOLID185.  10 node tets may also be used with SOLID186.  In either
! case, these elements are normally 8 sided brick elements arranged into a tetrahedral formation.

/COM ****************************************
/COM Define Material Variables and convert to appropriate units

DensitySI=916.7 	!kg/m^3
YoungsSI=2e9		!Pa, from Kermani et. al 2008
Poisson=0.325		!Dimensionless

Density=DensitySI*(1/1000)*(1/(1000**3))	!Density in Mg/mm^3 (tonne/mm^3)
Youngs=YoungsSI/(1e6)						!Youngs in MPa

MaxPrin=3	!MPa, Maximum principal strength of ice (tension)
MinPrin=5.5	!MPa, Minimum principal strength of ice (compression, enter abs value) from Kermani et. al 2007

!**********************************************
!**********************************************
TopDisp=-0.002	! Top Displacement in mm (either in shear or compression)
TopForce=0.003434	! Top normal force from weight of frame and snow (N)
		
TimeStep=abs(TopDisp)/0.2	! Sets the TimeStep equal to the displacement rate of lab tests (tests conducted at 0.2mm/sec)
endloop=25
!**********************************************
!**********************************************

/COM*******************************************
/COM Entering Main Routine -- SnowShear.inp --
/COM*******************************************

/INPUT,'SnowShear','inp','/home/djwalters/PhDWork/ANSYS/InputScripts',, 0

/COM*******************************************
/COM Finished Main Routine -- SnowShear.inp --
/COM*******************************************

/EOF