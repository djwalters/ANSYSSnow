/COM
/COM ****************************************
/COM ANSYS Mechanical APDL input file
/COM
/COM Author: David J Walters
/COM         Montana State University
/COM Version 1.0, 2014-04-10
/COM Version 2.0, 2014-06-24
/COM   Reworked restart functionality in relation to Element Death.
/COM Version 3.0, 2014-10-17
/COM   Rearranged so this becomes a called script with inputs coming from an outside wrapper script.
/COM
/COM
/COM Snow microstructure mechanical analysis utilizing yield strength as a damage criteria
/COM Loading is performed in any direction desired.
/COM NOTE: Academic License limits analyses to either 250k nodes or elements (whichever occurs first)
/COM
/COM Environment setup, variable initialization, and mesh import need to be handled before the call of this function
/COM This script applies material properties, and appropriate initial boundary conditions before entering the solution process.
/COM Solution process runs iteratively inspecting elements for exceeding yield strength following every solution step.
/COM If elements exceed yield, they are "Killed" from the analysis and new load steps are applied.
/COM The analsysis continues until 2 seperate bodies are created. Contact
/COM ****************************************
/COM


! All environment setup, variable initialization and mesh importation operations should be done before calling this file.
! The remaining steps here perform the analysis, and export of appropriate parameters.
! Enter preprocesser
/PREP7

! Perform element checks while selecting all that meet error criteria
CHECK,esel  
!EPLOT  

! Delete elements that meet error criteria, see .err log for details on error 
EDELE,all  

! Show model with coordinate triad in the bottom left 
ESEL,all
!EPLOT 
/TRIAD,LBOT
/VUP,,Z
/VIEW,,.5,-1,.5
!/REPLOT



! Define output array
*DIM,ForceOut,ARRAY,endloop,7,1

! Define material properties

! First delete properties already assigned
! Working only with 1 material, hence MAT = 1
MPDE,DENS,1 
MPDE,EX,1   
MPDE,PRXY,1 
! Assign new material properties as specified in the section above
MPDATA,DENS,1,,Density 
MPDATA,EX,1,,Youngs 
MPDATA,PRXY,1,,Poisson
KEYOPT,1,2,3  !Sets element type 1 (SOLID185) Keyopt2 to option 3.  
!This utilizes the simplified enhanced strain formulation of element technology to prevent shear locking.
!Option 2 is unnecessary since volumetric locking is not expected.

! Set boundary conditions

! Follwing set of commands locates the bottom set of nodes and sets them as a fixed condition
nsel,all
*get,minznode,NODE,0,mnloc,z
!nsel,s,loc,z,minznode,minznode+0.25 
!nplot  
!D,ALL,ALL,0  !Not applied since first loadstep should be zero

! Follwing set of commands locates the top set of nodes and sets them with the appropriate displacement
nsel,all
*get,maxznode,NODE,0,mxloc,z
NSEL,S,LOC,Z,maxznode-0.5,maxznode
*get,TopNodeCount,NODE,0,COUNT  
!nsel,s,loc,z,maxznode-0.5,maxznode 
!nplot  
!D,ALL,UZ,TopDisp	!Displaces top selected nodes down 0.1mm

nsel,all

FINISH

/SOLU
ANTYPE,0,NEW
NLGEOM,1
NROPT,FULL
NEQIT,30
!NSUBST,5
NCNV,2,0,0,0,0
!NCNV,1
! Added 2014-06-20
RESCONTROL,DEFINE,ALL,LAST,2
! Done adding
FINISH

!Turns off graphical solution tracking
/GST,OFF
!Begin saving frames for animation of element death
!/SEG,DELE  !Deletes all currently stored segments
!/SEG,MULTI

*DO,i,1,endloop,1		!Do loop setting the counter i from 2 to 3 in steps of 1
/GOPR
/SOLU


*IF,i,EQ,1,THEN
TIME,0.0001

*ELSE
TIME,(i-1)*TimeStep

*ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!
nsel,s,loc,z,minznode,minznode+0.25
D,ALL,ALL,0

NSEL,S,LOC,Z,maxznode-0.5,maxznode
*IF,i,EQ,1,THEN
F,ALL,FZ,-TopForce/TopNodeCount
*ELSE
D,ALL,UZ,(i-1)*TopDisp
F,ALL,FZ,-TopForce/TopNodeCount

*ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/COM FINISHED APPLYING LOADS, NOW SOLVE
NSEL,ALL
ESEL,ALL
SOLVE

*GET,Convergence,Active,0,Solu,CNVG

FINISH
!Work started on this section of code 2014-04-21

!Enter postprocessor to select failed elements.
!Kill failed elements based on max and min principle stresses
!Run next analysis step


/POST1
set,last
ETABLE,MaxPrinS,S,1	!Stores maximum principle stress in table labeled "MaxPrinS"
ETABLE,MinPrinS,S,3	!Stores minimum principle stress in table labeled "MinPrinS"
*GET,CurTime,Active,0,Set,TIME
nsel,s,loc,z,minznode,minznode+0.25

FSUM,FX
*GET,ReacFX,FSUM,0,ITEM,FX
FSUM,FY
*GET,ReacFY,FSUM,0,ITEM,FY
FSUM,FZ
*GET,ReacFZ,FSUM,0,ITEM,FZ
nsel,all
NSORT,U,X,0
*GET,MaxDispX,SORT,,MIN
nsel,all
NSORT,U,Y,0
*GET,MaxDispY,SORT,,MIN
nsel,all
NSORT,U,Z,0
*GET,MaxDispZ,SORT,,MIN
nsel,all

ForceOut(i,1)=CurTime
ForceOut(i,2)=MaxDispX
ForceOut(i,3)=MaxDispY
ForceOut(i,4)=MaxDispZ
ForceOut(i,5)=ReacFX
ForceOut(i,6)=ReacFY
ForceOut(i,7)=ReacFZ

!Plot current load step results for animation only showing the live elements
ESEL,S,LIVE
/CONT,,,-5,,3
PLESOL,S,3
ESEL,INVERT
/REPLOT
ESEL,ALL

!Now select elements to be killed
ESEL,S,ETAB,MaxPrinS,MaxPrin,,,0
ESEL,A,ETAB,MinPrinS,-1e26,-MinPrin,,0

!IMPORTANT!!!! Must save overwrite the rdb file after getting the ETABLEs for
!specifying element death.  Otherwise, the restart wipes them out.  This 
!function utilizes the current file's jobname as specified from the title of
!the input file.

SAVE,%JobString(1)%,rdb
FINISH

/SOLU

*IF,i,LT,endloop,THEN
ANTYPE,,REST
*ENDIF

EKILL,ALL
ESEL,ALL

FINISH
/PREP7
! Perform element checks while selecting all that meet error criteria
CHECK,esel  
!EPLOT  

! Delete elements that meet error criteria, see .err log for details on error 
EDELE,all   
ESEL,all

FINISH
*IF,Convergence,EQ,0,EXIT

*ENDDO

!/SEG,OFF
!ANIM,15,,0.5
!/ANFILE,SAVE

*MWRITE,ForceOut,CompForce,dat
(7G15.6)



/EOF